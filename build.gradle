buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.hidetake:gradle-ssh-plugin:2.9.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.0"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.0'
}
apply plugin: 'org.hidetake.ssh'
apply plugin: 'kotlin'

//def mainClass = 'example.HelloKotlinKt'
//def mainClass = 'example.GraalGameKt'
def mainClass = 'example.TicTacToeGameKt'

def ev3LibDir = '/home/lejos/lib/'
def ev3ProgramsDir = '/home/lejos/programs/'

remotes {
    localBrick {
        host = '10.0.1.1'
        user = 'root'
        password = 'root'
        authentications = ['password']
        knownHosts = allowAnyHosts
        fileTransfer = 'scp'
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.6"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.6"
    }
}

repositories {
    jcenter()
    maven { url = "https://jitpack.io" }
}

dependencies {
    compile 'com.github.mindstorms-cop.lejos-ev3-maven-packages:ev3classes:0.9.1-beta.+'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7"
}

jar {
    manifest {
        attributes(
            "Class-Path": configurations.compile.collect { ev3LibDir + it.getName() }.join(' '),
            "Main-Class": mainClass
        )
    }
}

task deployEv3 {
    dependsOn build
    doLast {
        ssh.run {
            session(remotes.localBrick) {
                put from: jar.archivePath, into: ev3ProgramsDir
            }
        }
    }
}

task deployEv3Libs {
    doLast {
        ssh.run {
            session(remotes.localBrick) {
                configurations.compile.forEach {
                    put from: it, into: ev3LibDir
                }
            }
        }
    }
}
